# Maintainer: Jelle van der Waa <jelle@vdwaa.nl>
# Contributor: Archist archist@die-optimisten.net
# Contributor: C Anthony Risinger
pkgname=('zarafa-server' 'zarafa-webaccess')
pkgver=7.2.0beta1
_pkgver=7.2.0
_pkgrel=47004
pkgrel=1
pkgdesc="Open Source Groupware Solution"
arch=('x86_64 i686')
url="http://www.zarafa.com/"
license=('AGPL3')
# TODO: add python-kyotocabinet for new search
makedepends=('libical' 'php>=5.2.0' 'e2fsprogs' 'openldap' 'automake' 'swig' 'zarafa-libvmime' 'mysql' 'python2' 'boost' 'boost-libs')
optdepends=('php: for webaccess')
install=${pkgname}.install
source=("zarafa-${pkgver}.tar.gz::http://download.zarafa.com/community/beta/7.2/${pkgver}-${_pkgrel}/sourcecode/zarafa-${_pkgver}.tar.gz"
	"zarafa-server.service"
	"zarafa-ical.service"
        "zarafa-gateway.service"
        "zarafa-spooler.service"
        "zarafa-dagent.service")

prepare() {
  cd ${srcdir}/zarafa-${_pkgver}
  # Fix python shebang
  sed -i 's/python setup.py/python2 setup.py/' swig/python/Makefile.in
  sed -i 's/python setup.py/python2 setup.py/' swig/python/Makefile.am
  sed -i 's/python setup.py/python2 setup.py/' swig/python/zarafa/Makefile.am
  sed -i 's/python setup.py/python2 setup.py/' swig/python/zarafa/Makefile.in
  sed -i 's/python setup.py/python2 setup.py/' ECtools/zarafa-search/Makefile.am
  sed -i 's/python setup.py/python2 setup.py/' ECtools/zarafa-search/Makefile.in
}

build() {
  cd ${srcdir}/zarafa-${_pkgver}

  msg "Starting build..."
  CPPFLAGS=-I/usr/include/python2.7 ./configure --prefix=/usr \
    --enable-python \
    --enable-swig \
    --with-python=/usr/bin/python2 \
    --disable-debug \
    --enable-unicode \
    --disable-static \
    --with-userscript-prefix=/etc/zarafa/userscripts \
    --with-quotatemplate-prefix=/etc/zarafa/quotamails \
    --with-searchscripts-prefix=/etc/zarafa/searchscripts

  make
}


package_zarafa-server() {
  pkgdesc="Open Source Groupware Solution"
  depends=('libical>=0.44' 'mysql' 'curl' 'libxml2' 'openssl' 'openldap' 'krb5' 'libical'
	 'boost>=1.52.0' 'bison' 'python2' 'swig' 'kyotocabinet' 'zarafa-libvmime' 'libmariadbclient')
  backup=('etc/zarafa/server.cfg'
	'etc/zarafa/dagent.cfg'
	'etc/zarafa/gateway.cfg'
	'etc/zarafa/monitor.cfg'
	'etc/zarafa/spooler.cfg'
	'etc/zarafa/ical.cfg')

  cd ${srcdir}/zarafa-${_pkgver}

  make DESTDIR=${pkgdir} install
  # Finalize
  install -m 755 -o root -g root -d ${pkgdir}/var/log/zarafa
  install -m 755 -o root -g root -d ${pkgdir}/var/lib/zarafa

  # move /var/lib to the right place related to https://jira.zarafa.com/browse/ZCP-12249
  #mv  $pkgdir/usr/var/lib/zarafa/* $pkgdir/var/lib/zarafa/
  #rm -rf $pkgdir/usr/var

  # Systemd files
  install -d  ${pkgdir}/usr/lib/systemd/system
  install -m 755 $srcdir/zarafa-server.service ${pkgdir}/usr/lib/systemd/system
  install -m 755 $srcdir/zarafa-ical.service ${pkgdir}/usr/lib/systemd/system
  install -m 755 $srcdir/zarafa-spooler.service ${pkgdir}/usr/lib/systemd/system
  install -m 755 $srcdir/zarafa-dagent.service ${pkgdir}/usr/lib/systemd/system
  install -m 755 $srcdir/zarafa-gateway.service ${pkgdir}/usr/lib/systemd/system

  # Remove unused /usr/etc related to https://jira.zarafa.com/browse/ZCP-12249
  #rm -rf $pkgdir/usr/etc

  # copy example configs to their active locations
  for cfg in ${pkgdir}/usr/share/doc/zarafa/example-config/*.cfg; do
    install -m 755 -o root -g root -D ${cfg} ${pkgdir}/etc/zarafa
  done
}

package_zarafa-webaccess() {
  pkgdesc="Webaccess client for the Zarafa Open Source Groupware Solution"
  depends=('zarafa-server')

  # Webaccess
  install -d ${pkgdir}/etc/httpd/conf/extra/
  install -m 755 ${srcdir}/zarafa-${_pkgver}/php-webclient-ajax/zarafa-webaccess.conf ${pkgdir}/etc/httpd/conf/extra/zarafa-webaccess.conf
  install -m 755 -d ${pkgdir}/usr/share/zarafa-webaccess/

  cp -r ${srcdir}/zarafa-${_pkgver}/php-webclient-ajax/* ${pkgdir}/usr/share/zarafa-webaccess/
  mv ${pkgdir}/usr/share/zarafa-webaccess/config.php.dist ${pkgdir}/usr/share/zarafa-webaccess/config.php
}
md5sums=('ba460374f6678ba8c6412ad93b7a8181'
         '0e2728f1e35b25ca679427fcb57315d8'
         'c25f3982217fe390d68ed9003a5988ed'
         '705ada3a8c4b904696e0c461c131b4f7'
         '846b76e5cb0239a488c81e11d74ad08b'
         '9666bf713645af11dd65b3ac5cbb42d9')
