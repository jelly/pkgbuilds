# Maintainer: Jelle van der Waa <jelle@vdwaa.nl>
# Contributor: Archist archist@die-optimisten.net
# Contributor: C Anthony Risinger

pkgname=zarafa-webapp
pkgver=1.5
pkgrel=1
_svnver=43477
pkgdesc="WebApp for Zarafa"
arch=('any')
url="http://www.zarafa.com/"
license=('AGPL3')
makedepends=('apache-ant')
depends=('php')
source=("webapp-${_svnver}.tar.gz::http://download.zarafa.com/community/final/WebApp/${pkgver}/webapp-${pkgver}.svn${_svnver}.tar.gz")
md5sums=('554f7baa087ce948f12c6898ee3c4611')

build() {
  cd ${srcdir}/webapp-${pkgver}.svn${_svnver}
  ant deploy
  ant deploy-plugins
}

package() {
  cd ${srcdir}/webapp-${pkgver}.svn${_svnver}/deploy

  install -d ${pkgdir}/usr/share/zarafa-webapp/

  install -d ${pkgdir}/var/lib/zarafa-webapp/tmp
  chown http ${pkgdir}/var/lib/zarafa-webapp/tmp

  install -d ${pkgdir}/etc/httpd/conf/extra
  install -p zarafa-webapp.conf ${pkgdir}/etc/httpd/conf/extra/

  #install debug.php.dist ${pkgdir}/usr/share/zarafa-webapp/debug.php
  install config.php.dist ${pkgdir}/usr/share/zarafa-webapp/config.php
  install *.php ${pkgdir}/usr/share/zarafa-webapp/
  cp -r client ${pkgdir}/usr/share/zarafa-webapp/
  cp -r mapi ${pkgdir}/usr/share/zarafa-webapp/
  cp -r plugins ${pkgdir}/usr/share/zarafa-webapp/
  cp -r server ${pkgdir}/usr/share/zarafa-webapp/
}
